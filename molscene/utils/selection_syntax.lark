// --------------------------
// Imports & Ignored
// --------------------------
%import common.CNAME
%import common.SIGNED_NUMBER
%import common.ESCAPED_STRING
%import common.WS_INLINE
%ignore WS_INLINE

// Ignore Python-style comments
COMMENT: /#[^\n]*/
%ignore COMMENT

// ---------------------------
// LOGICAL OPERATIONS
// ---------------------------

?start: expr

// Logical precedence (OR < XOR < AND < NOT)
?expr: or_expr

?or_expr: xor_expr
        | or_expr OR_OP xor_expr      -> or_

?xor_expr: and_expr
         | xor_expr XOR_OP and_expr   -> xor_

?and_expr: not_expr
         | and_expr AND_OP not_expr   -> and_
         | not_expr not_expr   -> and_

?not_expr: NOT_OP not_expr            -> not_
         | atom

?atom: "(" expr ")"
     | selection

// --------------------------
// Selection Grammar
// --------------------------

?selection: bool_keyword
          | comparison_sel
          | range_or_property_sel
          | regex_sel
          | within_sel
          | bonded_sel
          | sequence_sel
          | same_sel
          | "(" selection ")"

//Range selection
comparison_sel: math_expr (comparison_op math_expr)+   -> comparison_selection

// Left-factored list-or-range
?range_or_property_sel.10: math_expr range_branch                -> range_selection
                         | math_expr selection_value+            -> property_selection

// the three ways to write a numeric range
range_branch: number_expr TO number_expr
            | number_expr COLON number_expr
            | number_expr COLON number_expr COLON number_expr

// Regex selection
regex_sel: selection_keyword REGEXP_OP ESCAPED_STRING -> regex_selection

// Within selection
?within_sel: whithin_op number_expr OF expr     -> within_selection

// Bonded selection
?bonded_sel: bonded_op number_expr TO expr -> bonded_selection

// Sequence selection
sequence_value: /[a-zA-Z]+/
?sequence_sel: sequence_op ESCAPED_STRING -> sequence_selection_regex
             | sequence_op sequence_value -> sequence_selection

// Same selection
same_sel: same_op selection_keyword AS expr -> same_selection

?selection_value.-10: ESCAPED_STRING -> _escaped_string
                    | SIGNED_NUMBER  -> _number
                    | VALUE          -> _value   

// Property selection
?property_single: math_expr number_expr
// property_list.-10: property_single (COMMA? selection_value)+        -> property_list

// --------------------------
// Math Grammar
// --------------------------

// Math expression with variables
?math_expr: sum

?sum: product
    | sum PLUS product              -> add
    | sum MINUS product             -> sub

?product: power
        | product TIMES power       -> mul
        | product DIV power         -> div
        | product INTEGER_DIV power -> int_div
        | product MOD power         -> mod

?power: value
      | power POW value             -> pow

?value: SIGNED_NUMBER               -> number
      | constant                      -> const
      | "-" value                   -> neg
      | function_name "(" math_expr ")" -> func
      | selection_keyword
      | "(" math_expr ")"

// Math expression with no variables
?number_expr: number_sum

?number_sum: number_product
           | number_sum PLUS number_product        -> add
           | number_sum MINUS number_product       -> sub

?number_product: number_power
               | number_product TIMES number_power -> mul
               | number_product DIV number_power   -> div
               | number_product INTEGER_DIV number_power -> int_div
               | number_product MOD number_power   -> mod

?number_power: number_value
             | number_power POW number_value       -> pow

?number_value: SIGNED_NUMBER                       -> number
             | constant                            -> const
             | "-" number_value                    -> neg
             | function_name "(" math_expr ")"     -> func
             | "(" number_sum ")"

// --------------------------
// Boolean Keywords
// --------------------------

// Universal keywords
ALL        : "all" | "everything"                 
NONE       : "none" | "nothing"                   

?bool_universal: ALL | NONE

// Universal keywords
PROTEIN    : "protein" | "is_protein"
NUCLEIC    : "nucleic" | "is_nucleic"
DNA        : "dna" | "is_dna"
RNA        : "rna" | "is_rna"
WATER      : "water" | "waters" | "is_water"
LIPID      : "lipid" | "lipids"
ION        : "ion" | "ions"
SUGAR      : "sugar"
GLYCAN     : "glycan"
HEME       : "heme"
DRUDE      : "drude"
SOLVENT    : "solvent"

?bool_biomolecule: PROTEIN | NUCLEIC | DNA | RNA | WATER | LIPID | ION | SUGAR | GLYCAN | HEME | DRUDE | SOLVENT

// Residue Type
AROMATIC   : "aromatic"
ALIPHATIC  : "aliphatic"
POLAR      : "polar"
HYDROPHOBIC: "hydrophobic"
CHARGED    : "charged"
ACIDIC     : "acidic"
BASIC      : "basic"
NEUTRAL    : "neutral"
ACYCLIC    : "acyclic"
CYCLIC     : "cyclic"
AMINO      : "amino"
PURINE     : "purine"
PYRIMIDINE : "pyrimidine"
HETERO     : "hetero"
CG         : "cg"
TA         : "tA"

?bool_residue: AROMATIC | ALIPHATIC | POLAR | HYDROPHOBIC | CHARGED | ACIDIC | BASIC | NEUTRAL | ACYCLIC | CYCLIC | AMINO | PURINE | PYRIMIDINE | HETERO | CG | TA

// Atom Type
CARBON     : "carbon"
HYDROGEN   : "hydrogen"
NITROGEN   : "nitrogen"
OXYGEN     : "oxygen"
SULFUR     : "sulfur"
PHOSPHORUS : "phosphorus"
FLUORINE   : "fluorine"
CHLORINE   : "chlorine"
BROMINE    : "bromine"
IODINE     : "iodine"
METAL      : "metal"
NOH        : "noh"
HEAVY      : "heavy"

?bool_atom: CARBON | HYDROGEN | NITROGEN | OXYGEN | SULFUR | PHOSPHORUS | FLUORINE | CHLORINE | BROMINE | IODINE | METAL | NOH | HEAVY

// Structural Role
CALPHA   : "calpha" | "ca" | "alpha"
BACKBONE   : "backbone" | "is_backbone"
SIDECHAIN  : "sidechain" | "is_sidechain"
// SUGAR      : "sugar" | "is_sugar"
// PHOSPHATE  : "phosphate" | "is_phosphate"
// NUCLEOBASE : "nucleobase" | "is_nucleobase"
// STRUCTURE  : "structure"

?bool_structure: BACKBONE | SIDECHAIN | CALPHA

// Size / Exposure
SMALL      : "small"
MEDIUM     : "medium"
LARGE      : "large"

?bool_size: SMALL | MEDIUM | LARGE

// Exposure

BURIED     : "buried"
SURFACE    : "surface"

?bool_exposure: BURIED | SURFACE

// Secondary Structure
HELIX          : "helix"
SHEET          : "sheet"
TURN           : "turn"
COIL           : "coil"
ALPHA_HELIX    : "alpha_helix"
PI_HELIX       : "pi_helix"
HELIX310       : "helix_3_10"
EXTENDED_BETA  : "extended_beta"
BRIDGE_BETA    : "bridge_beta"

?bool_secondary: ALPHA_HELIX | PI_HELIX | HELIX | HELIX310 | EXTENDED_BETA | BRIDGE_BETA | SHEET | TURN | COIL

MACRO: "@" CNAME

bool_macro: MACRO -> macro_sel

bool_keyword: bool_universal | bool_biomolecule | bool_residue | bool_atom | bool_structure | bool_size | bool_secondary| bool_macro

// --------------------------
// Tokens
// --------------------------

// Regex match
REGEXP_OP.1 : "=~"                 

// Not
NOT_OP    : "not"  | "!"        

// LOGICAL OPERATORS
OR_OP     : "or"   | "||" | "|"
AND_OP    : "and"  | "&&" | "&"
XOR_OP    : "xor"         

?logical_op: OR_OP | AND_OP | NOT_OP

// COMPARISON OPERATORS
LE        : "<="   | "le"     
GE        : ">="   | "ge"     
EQ        : "=="   | "=" | "eq" 
LT        : "<"    | "lt"       
GT        : ">"    | "gt"       
NE        : "!="   | "ne"       
?comparison_op: LE | GE | EQ | LT | GT | NE

// RANGE OPERATORS
     
COLON     : ":"           
COMMA     : ","           
?range_op : TO | COLON | COMMA

// COMPARISON TOKENS
SAME      : "same"
AS        : "as"
WITHIN    : "within"
EXWITHIN  : "exwithin"
OF        : "of"
BONDED    : "bonded"
EXBONDED  : "exbonded"
TO      : "to"    
SEQUENCE  : "sequence"
?whithin_op: WITHIN | EXWITHIN
?bonded_op : BONDED | EXBONDED
?same_op: SAME
?sequence_op: SEQUENCE

// MATH OPERATORS
PLUS        : "+"           
MINUS       : "-"           
TIMES       : "*"           
DIV         : "/"           
INTEGER_DIV : "//"          
MOD         : "%"           
POW         : "**" | "^"
?math_op : PLUS | MINUS | TIMES | DIV | MOD | POW

?operation_keyword: math_op
                | logical_op
                | comparison_op
                | range_op
                | REGEXP_OP
                | NOT_OP


// --------------------------
// Selection Keywords
// --------------------------

// Atom Properties
BETA      : "beta"
OCCUPANCY : "occupancy"
ALTLOC    : "altloc"
MASS      : "mass"
CHARGE    : "charge"
RADIUS    : "radius"
ELEMENT   : "type" | "element" | "symbol"

?select_property: BETA | OCCUPANCY | ALTLOC | MASS | CHARGE | RADIUS | ELEMENT

// Vector Information
X        : "x"
Y        : "y"
Z        : "z"
FX       : "fx"
FY       : "fy"
FZ       : "fz"
VX       : "vx"
VY       : "vy"
VZ       : "vz"

?select_position: X | Y | Z

// --------------------------
// Indexing / Codes / Labels
// --------------------------
CODE     : "code" | "rescode" | "resc"
NAME_KW  : "name"
INDEX_KW : "index"

?select_indexing: CODE | NAME_KW | INDEX_KW

// --------------------------
// Connectivity / Fragmentation
// --------------------------
BONDS    : "bonds" | "numbonds"
FRAGMENT : "fragment"
PFRAG    : "pfrag"
NFRAG    : "nfrag"

?select_fragment: BONDS | FRAGMENT | PFRAG | NFRAG

// --------------------------
// Residue Information
// --------------------------
RESIDUE    : "residue" | "resSeq"
RESNAME    : "resname" | "resn"
RESID      : "resid" | "resi"
CHAINID    : "chainid"
SEGMENT_ID : "segment_id" | "segname"
CONFORMATION : "conformation"

?select_residue: RESIDUE | RESNAME | RESID | CHAINID | SEGMENT_ID | CONFORMATION

VARIABLE  : "$"CNAME

select_variable: VARIABLE -> var_sel

?selection_keyword: select_property
                  | select_position
                  | select_indexing
                  | select_fragment
                  | select_residue
                  | select_variable

// ----------------------------
// MATH KEYWORDS
// ----------------------------

// Math function names (as terminals, so they're easy to recognize)
SIN     : "sin"
COS     : "cos"
TAN     : "tan"
ASIN    : "asin"
ACOS    : "acos"
ATAN    : "atan"
EXP     : "exp"
LOG     : "log"
LOG10   : "log10"
SQRT    : "sqrt"
SQUARE  : "sq"
ABS     : "abs"

?function_name: SIN | COS | TAN | ASIN | ACOS | ATAN | EXP | LOG | LOG10 | SQRT | SQUARE | ABS

// --------------------------
// Constants
// --------------------------

PI      : "pi"
E       : "e"

?constant: PI | E

//--------------------------
// Values
// --------------------------


//VALUE.-10: /(?!(to|:)\b)[^\s()]+/       // Last resort for anything not matched by other rules (most greedy)
//VALUE.-10: /(?![-'"()])(?!(?:all|none|protein|nucleic|dna|rna|water|lipid|ion|sugar|glycan|heme|drude|solvent|aromatic|aliphatic|polar|hydrophobic|charged|acidic|basic|neutral|acyclic|cyclic|amino|purine|pyrimidine|hetero|cg|tA|carbon|hydrogen|nitrogen|oxygen|sulfur|phosphorus|fluorine|chlorine|bromine|iodine|metal|noh|heavy|calpha|backbone|sidechain|small|medium|large|buried|surface|helix|sheet|turn|coil|alpha_helix|pi_helix|helix_3_10|extended_beta|bridge_beta|@|=\~|not|or|and|xor|<=|>=|==|<|>|!=|:|,|same|as|within|exwithin|of|bonded|exbonded|to|sequence|\+|\-|\*|\/|\/\/|%|\*\*|beta|occupancy|altloc|mass|charge|radius|type|\$|x|y|z|fx|fy|fz|vx|vy|vz|code|name|index|bonds|fragment|pfrag|nfrag|residue|resname|resid|chainid|segment_id|conformation|sin|cos|tan|asin|acos|atan|exp|log|log10|sqrt|sq|abs)\b)[^()'"\s]+/       // Last resort for anything not matched by other rules (most greedy)
VALUE.-10: /(?![-'"()])(?!(?:all|everything|none|nothing|protein|is_protein|nucleic|is_nucleic|dna|is_dna|rna|is_rna|water|waters|is_water|lipid|lipids|ion|ions|sugar|glycan|heme|drude|solvent|aromatic|aliphatic|polar|hydrophobic|charged|acidic|basic|neutral|acyclic|cyclic|amino|purine|pyrimidine|hetero|cg|tA|carbon|hydrogen|nitrogen|oxygen|sulfur|phosphorus|fluorine|chlorine|bromine|iodine|metal|noh|heavy|calpha|ca|alpha|backbone|is_backbone|sidechain|is_sidechain|small|medium|large|buried|surface|helix|sheet|turn|coil|alpha_helix|pi_helix|helix_3_10|extended_beta|bridge_beta|@|=\~|not|!|or|\|\||\||and|\&\&|\&|xor|<=|le|>=|ge|==|=|eq|<|lt|>|gt|!=|ne|:|,|same|as|within|exwithin|of|bonded|exbonded|to|sequence|\+|\-|\*|\/|%|\*\*|\^|beta|occupancy|altloc|mass|charge|radius|type|element|symbol|\$|x|y|z|fx|fy|fz|vx|vy|vz|code|rescode|resc|name|index|bonds|numbonds|fragment|pfrag|nfrag|residue|resSeq|resname|resn|resid|resi|chainid|segment_id|segname|conformation|sin|cos|tan|asin|acos|atan|exp|log|log10|sqrt|sq|abs|pi|e)\b)(?!\d+(?:\.\d*)?(?:[eE][+-]?\d+)?\b)(?=[A-Za-z])[^()'"\s]+/   // Last resort for anything not matched by other rules (most greedy)

//TODO: Allow comparisons == and != to be used with strings
//TODO: Split rules for numeric properties and string properties 
//TODO: Create rules for errors
